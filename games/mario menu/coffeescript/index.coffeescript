# Best in Chrome, Safari - Firefox has some glitchy control issues.

$ ->

  mario = {
    element: $('.mario'),
    bottom: 40,
    left: 100
  }
  move_times = 10
  x_movement = null
  x_movement_on = false
  x_movement_dir = 0
  y_movement = null
  window_width = $(window).width()

  move_degree = (d) ->
    d * move_times

  collision = ($div1, $div2) ->
    x1 = $div1.offset().left
    y1 = $div1.offset().top
    h1 = $div1.outerHeight(true)
    w1 = $div1.outerWidth(true)
    b1 = y1 + h1
    r1 = x1 + w1
    x2 = $div2.offset().left
    y2 = $div2.offset().top
    h2 = $div2.outerHeight(true)
    w2 = $div2.outerWidth(true)
    b2 = y2 + h2
    r2 = x2 + w2

    if b1 < y2 || y1 > b2 || r1 < x2 || x1 > r2 
      false
    else
      true


  mario_event_listener = () ->
    $('.menu .option').each (index) ->
      if collision($('.mario'), $(@)) && !$(@).hasClass 'touch'
        $('.menu .option.touch').removeClass 'touch'
        $(@).addClass 'touch'
        $(".content .show").removeClass 'show'
        $(".content .content-index-#{index}").addClass 'show'

  move_mario = (x,y) ->

    new_x = mario.left + x
    new_x = 5 if new_x < 5

    new_y = mario.bottom + y
    new_y = 40 if new_y < 40
    new_y = 190 if new_y > 190

    mario_event_listener()

    mario.bottom = new_y
    mario.left = new_x
    mario.element.css
      bottom: mario.bottom
      left: mario.left

  start_movement = (x,y) ->

    # allow rapid change of direction
    if x != 0 && x_movement_on && x_movement_dir != x
      end_movement('x')

    if x != 0 && !x_movement_on
      mario.element.addClass('left') if x < 0
      mario.element.addClass('right') if x > 0
      x_movement_dir = x
      x_movement = setInterval ->
        move_mario move_degree(x), 0
      , 100
      x_movement_on = true
    else if y != 0
      y_movement = setInterval ->
        move_mario 0, move_degree(y)
      , 100

  end_movement = (axis) ->
    if axis == 'x'
      mario.element.removeClass('left')
      mario.element.removeClass('right')
      clearInterval(x_movement)
      x_movement_on = false
    else if axis == 'y'
      clearInterval(y_movement)

  $(window).keydown (e) ->
    if e.which ==  65 || e.which ==  37
      start_movement -4, 0
    else if e.which == 68 || e.which ==  39
      start_movement 4, 0
    else if e.which == 32
      move_mario 0, move_degree(20) if mario.bottom == 40 # must be on the ground to jump

  $('.content').click ->
    if $('.first-screen').hasClass 'show'
      $('.first-screen').removeClass 'show'
      $('.getting-started').addClass 'show'
  
  $(window).keyup (e) ->
    # only stop the animation if the current movement matches that of the button being released, if a change of direction had happened, we want to ignore this button release
    if e.which ==  65 || e.which ==  37
      end_movement 'x' unless x_movement_dir > 0 
    else if e.which == 68 || e.which ==  39
      end_movement 'x' unless x_movement_dir < 0

  setInterval () ->
    move_mario 0, move_degree(-2) unless mario.bottom == 40 # gravity
  , 40

  setInterval () ->
    if mario.element.hasClass 'alternate'
      mario.element.removeClass 'alternate'
    else
      mario.element.addClass 'alternate'
  , 80
